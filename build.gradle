buildscript {
    dependencies {
        classpath group: 'org.sonarsource.scanner.gradle', name: 'sonarqube-gradle-plugin', version: '2.5'
        classpath("net.serenity-bdd:serenity-gradle-plugin:2.0.11")
    }
}

plugins {
    id 'application'
    id 'pmd'
    id 'io.spring.dependency-management' version '1.0.6.RELEASE'
    id 'org.springframework.boot' version '2.1.4.RELEASE'
    id 'org.owasp.dependencycheck' version '5.0.0'
    id 'com.github.ben-manes.versions' version '0.15.0'
    id 'com.gorylenko.gradle-git-properties' version '1.5.2'
    id 'info.solidsoft.pitest' version '1.3.0'
    id 'au.com.dius.pact' version '3.5.24'
}

apply plugin: 'org.sonarqube'
apply plugin: 'findbugs'
apply plugin: 'jacoco'
apply plugin: 'net.serenity-bdd.aggregator'


gitProperties {
    gitPropertiesDir = new File("${project.rootDir}/src/main/resources/uk/gov/hmcts/probate/services/business")
    keys = ['git.commit.id','git.commit.time']
    dateFormat = "yyyy-MM-dd'T'HH:mmZ"
    dateFormatTimeZone = "GMT"
}

group = 'uk.gov.hmcts.probate'
version = "3.0.0"
mainClassName = 'uk.gov.hmcts.probate.BusinessApplication'

sourceCompatibility = 1.8
targetCompatibility = 1.8

//check.dependsOn dependencyCheckAnalyze

jacoco {
    toolVersion = '0.8.0'
}

jacocoTestReport {
    group = "Reporting"
    reports {
        xml.enabled false
        html.destination file("${buildDir}/reports/jacoco")
    }
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}


findbugs{
    ignoreFailures=true
    reportLevel = "low"
}

sonarqube {
    properties {
        property "sonar.projectName", "Probate :: Business Service"
        property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/test.exec"
        property "sonar.host.url", "https://sonar.reform.hmcts.net/"
        property "sonar.pitest.mode", "reuseReport"
        property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    }
}
pitest {
    targetClasses = ['uk.gov.hmcts.probate.*']
    threads = 4
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
    mutationThreshold = 50}

test.finalizedBy jacocoTestReport

dependencyCheck {
    failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
    suppressionFiles = ['config/owasp/suppressions.xml']
    analyzers {
        assemblyEnabled = false
    }
    scanConfigurations = ['compile']
}

sourceSets {
    testFunctional {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/functionalTest/java')
        }
    resources.srcDir file('src/functionalTest/resources')
  }

    testSmoke {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/smokeTest/java')
        }
        resources.srcDir file('src/smokeTest/resources')
    }

    testContract {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/contractTest/java')
        }
        resources.srcDir file('src/contractTest/resources')
    }
}

task smoke(type: Test) {
    description = "Runs Smoke Tests"
    testClassesDirs = sourceSets.testSmoke.output.classesDirs
    classpath = sourceSets.testSmoke.runtimeClasspath
}

task functional(type: Test) {
    description = "Runs functional Tests"
    testClassesDirs = sourceSets.testFunctional.output.classesDirs
    classpath = sourceSets.testFunctional.runtimeClasspath
    finalizedBy aggregate
}

task pactTask(type: Test) {
    description = "Runs pact Tests"
    testClassesDirs = sourceSets.testContract.output.classesDirs
    classpath = sourceSets.testContract.runtimeClasspath
}


pmd {
    toolVersion = "6.3.0"
    ignoreFailures = true
    sourceSets = [sourceSets.main, sourceSets.test, sourceSets.testFunctional, sourceSets.testSmoke]
    reportsDir = file("$project.buildDir/reports/pmd")
    ruleSetFiles = files("config/pmd/ruleset.xml")
}

jacocoTestReport {
    executionData(test)
    reports {
        xml.enabled = true
        csv.enabled = false
        xml.destination = file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
    }
}

repositories {
    mavenLocal()
    maven { url "https://dl.bintray.com/hmcts/hmcts-maven" }
    maven { url 'https://repo.spring.io/libs-milestone' }
    maven { url "http://repo.maven.apache.org/maven2" }
    jcenter()
}

processResources {
    from ('src/lib/AI-Agent.xml') {
        into 'lib'
    }
}

def versions = [
    logging: '3.0.2'
]

dependencies {
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web-services'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    compile group: 'org.springframework.retry', name: 'spring-retry'
    compile group: 'org.projectlombok', name: 'lombok', version: '1.16.18'
    compile group: 'org.springframework.hateoas', name: 'spring-hateoas'
    compile group: 'org.hibernate.validator', name: 'hibernate-validator'
    compile group: 'uk.gov.service.notify', name: 'notifications-java-client', version: '3.8.0-RELEASE'
    compile group: 'uk.gov.hmcts.reform', name: 'java-logging', version: versions.logging
    compile group: 'uk.gov.hmcts.reform', name: 'java-logging-spring', version: versions.logging
    compile group: 'uk.gov.hmcts.reform', name: 'java-logging-appinsights', version: versions.logging
    compile group: 'uk.gov.hmcts.reform', name: 'java-logging-httpcomponents', version: versions.logging
    compile group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: '1.0.4'
    compile group: 'uk.gov.hmcts.reform', name: 'pdf-service-client', version: '6.0.0'
    compile group: 'uk.gov.hmcts.reform', name: 'document-management-client', version: '6.0.0'
    compile group: 'uk.gov.hmcts.reform', name: 'health-spring-boot-starter', version: '0.0.4'
    compile group: 'uk.gov.hmcts.reform', name: 'properties-volume-spring-boot-starter', version: '0.0.4'
    compile group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.0.1'
    compile group: 'io.springfox', name: 'springfox-swagger2', version: '2.7.0'
    compile group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.7.0'
    compile group: 'org.pitest', name: 'pitest', version: '1.3.2'
    compile 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.3.0'
    compile 'org.codehaus.sonar-plugins:sonar-pitest-plugin:0.5'
    compile ("com.fasterxml.jackson.core:jackson-databind:2.9.8") {
        force = true
    }
    compile group: 'uk.gov.hmcts.reform.probate', name: 'probate-commons', version: '0.0.44'
    
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    testCompile group: 'org.apache.pdfbox', name: 'pdfbox', version: '2.0.12'

    testFunctionalCompile group: 'net.serenity-bdd', name: 'serenity-core', version: '2.0.43'
    testFunctionalCompile group: 'net.serenity-bdd', name: 'serenity-junit', version: '2.0.43'
    testFunctionalCompile group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: '2.0.43'
    testFunctionalCompile group: 'net.serenity-bdd', name: 'serenity-spring', version: '2.0.43'
    testFunctionalCompile group: 'io.jsonwebtoken', name: 'jjwt', version: '0.9.0'
    testFunctionalCompile group: 'uk.gov.hmcts.reform', name: 'pdf-service-client', version: '6.0.0'
    testFunctionalCompile group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: '1.0.4'
    testFunctionalCompile sourceSets.main.runtimeClasspath
    testFunctionalCompile sourceSets.test.runtimeClasspath

    testCompile group: 'io.rest-assured', name: 'rest-assured', version: '3.0.7'
    testSmokeCompile sourceSets.main.runtimeClasspath
    testSmokeCompile sourceSets.test.runtimeClasspath
    
    testContractCompile group: 'io.jsonwebtoken', name: 'jjwt', version: '0.9.0'
    testContractCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    testCompile  group: 'au.com.dius', name: 'pact-jvm-provider-spring_2.12', version: '3.5.24'
    testContractCompile  group: 'au.com.dius', name: 'pact-jvm-provider-spring_2.12', version: '3.5.24'
    testContractCompile("org.junit.jupiter:junit-jupiter-api:5.3.2")
    testContractRuntime("org.junit.jupiter:junit-jupiter-engine:5.3.2")
    testContractImplementation('org.junit.jupiter:junit-jupiter-api:5.3.2')
    testContractCompile sourceSets.main.runtimeClasspath
    testContractCompile sourceSets.test.runtimeClasspath
}

compileJava {
    options.compilerArgs << '-parameters'
}

bootJar {
    archiveName = 'business-service.jar'
    manifest {

        attributes 'Implementation-Title': project.name,
                'Implementation-Version': project.version
    }
}

task printVersion {
    doLast {
        print project.version
    }
}

run {
    if (debug == 'true') {
        jvmArgs = ['-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=5005']
    }
}
